// A grammar for j--, suitable for parsing by recursive descent
// Copyright 2013 Bill Campbell, Swami Iyer and Bahar Akbal-Delibas

compilationUnit ::= [PACKAGE qualifiedIdentifier SEMI]
                    {IMPORT  qualifiedIdentifier SEMI}
                    {typeDeclaration} EOF 

qualifiedIdentifier ::= IDENTIFIER {DOT IDENTIFIER}

typeDeclaration ::= modifiers (classDeclaration | interfaceDeclaration)

modifiers ::= {PUBLIC | PROTECTED | PRIVATE | STATIC | ABSTRACT}

classDeclaration ::= CLASS IDENTIFIER 
                       [EXTENDS qualifiedIdentifier] 
                       [IMPLEMENTS qualifiedIdentifier {COMMA qualifiedIdentifier}] 
                       classBody

interfaceDeclaration ::= INTERFACE IDENTIFIER 
                       [EXTENDS qualifiedIdentifier {COMMA qualifiedIdentifier}] 
                       interfaceBody

classBody ::= LCURLY {modifiers classMemberDecl} RCURLY

interfaceBody ::= LCURLY {modifiers interfaceMemberDecl} RCURLY

classMemberDecl ::= IDENTIFIER            // constructor
                 formalParameters block throwTypes
             | (VOID | type) IDENTIFIER  // method
                 formalParameters throwTypes
                 (block | SEMI)
             | type variableDeclarators SEMI // field
             | block // initializing field

interfaceMemberDecl ::=  (VOID | type) IDENTIFIER  // method
							 formalParameters throwTypes
							 SEMI
                		 | type variableDeclarators SEMI // field

block ::= LCURLY {blockStatement} RCURLY 

blockStatement ::= localVariableDeclarationStatement
                 | statement

statement ::= block
            | IF parExpression statement [ELSE statement]
            | WHILE parExpression statement 
            | FOR forExpression statement
            | TRY statement {CATCH catchExpression statement} [FINALLY statement]
            | THROW expression SEMI
            | RETURN [expression] SEMI
            | SEMI 
            | statementExpression SEMI

formalParameters ::= LPAREN 
                       [formalParameter {COMMA formalParameter}]
                     RPAREN

formalParameter ::= type IDENTIFIER

throwTypes ::= [THROWS qualifiedIdentifier {COMMA qualifiedIdentifier}]

parExpression ::= LPAREN expression RPAREN

catchExpression ::= LPAREN formalParameter RPAREN

localVariableDeclarationStatement ::= type variableDeclarators SEMI

variableDeclarators ::= variableDeclarator {COMMA variableDeclarator}

variableDeclarator ::= IDENTIFIER [ASSIGN variableInitializer]
                    
variableInitializer ::= arrayInitializer | expression

arrayInitializer ::= LCURLY 
                       [variableInitializer {COMMA variableInitializer}]
                     RCURLY 

arguments ::= LPAREN [expression {COMMA expression}] RPAREN

type ::= referenceType | basicType

basicType ::= BOOLEAN | CHAR | INT | BYTE | DOUBLE | FLOAT | LONG | SHORT

referenceType ::= basicType LBRACK RBRACK {LBRACK RBRACK}
                | qualifiedIdentifier {LBRACK RBRACK}

statementExpression ::= expression // but must have side-effect, eg i++

forExpression ::= LPAREN [[type] variableDeclarator] SEMI [expression] SEMI [expression] RPAREN // basic for
				| LPAREN formalParameter COLON expression RPAREN // enhanced for loop

expression ::= assignmentExpression | conditionalExpression

conditionalExpression ::= assignmentExpression TERN assignmentExpression COLON assignmentExpression

assignmentExpression ::= conditionalExpression  // must be a valid lhs
                           [(ASSIGN | PLUS_ASSIGN | MINUS_ASSIGN | STAR_ASSIGN | DIV_ASSIGN | REM_ASSIGN | AND_ASSIGN | LNOT_ASSIGN | XOR_ASSIGN | OR_ASSIGN | LSHIFT_ASSIGN | RSHIFT_ASSIGN | LRSHIFT_ASIGN ) assignmentExpression]

conditionalExpression ::= bitwiseExpressionFour // level 10
                               {LAND | LOR bitwiseExpressionFour}                               
                               
bitwiseExpressionFour ::= bitwiseExpressionThree		// level 9
						{OR bitwiseExpressionThree}

bitwiseExpressionThree ::= bitwiseExpressionTwo		// level 8
						{XOR bitwiseExpressionTwo}

bitwiseExpressionTwo ::= equalityExpression		// level 7
						{AND equalityExpression}

equalityExpression ::= relationalExpression  // level 6
                         {EQUAL relationalExpression}

relationalExpression ::= bitwiseExpressionOne       // level 5
                           [(GT | LS | GE | LE) bitwiseExpressionOne
                           | INSTANCEOF referenceType]
                           
bitwiseExpressionOne ::= additiveExpression		// level 4
						{(LSHIFT | RSHIFT | LRSHIFT) additiveExpression}

additiveExpression ::= multiplicativeExpression // level 3
                         {(PLUS | MINUS) multiplicativeExpression}
                               
multiplicativeExpression ::= unaryExpression // level 2
								{(STAR | DIV | REM) unaryExpression }
								
unaryExpression ::= INC unaryExpression  // level 1
				  | DEC unaryExpression
                  | MINUS unaryExpression
                  | simpleUnaryExpression

simpleUnaryExpression ::= LNOT unaryExpression
		                | PLUS unaryExpression
		                | NOT unaryExpression
                        | LPAREN basicType RPAREN unaryExpression //casted
                        | LPAREN // casted               
                            referenceType
                          RPAREN simpleUnaryExpression
                        | postfixExpression 

postfixExpression ::= primary {selector} {DEC|INC}

selector ::= DOT qualifiedIdentifier [arguments]
           | LBRACK expression RBRACK

primary ::= parExpression
          | THIS [arguments]
          | SUPER ( arguments 
                  | DOT IDENTIFIER [arguments] 
                  )
          | literal
          | NEW creator
          | qualifiedIdentifier [arguments]

creator ::= (basicType | qualifiedIdentifier) 
              ( arguments
              | LBRACK RBRACK {LBRACK RBRACK} [arrayInitializer]
              | newArrayDeclarator
              )

newArrayDeclarator ::= LBRACK expression RBRACK 
                         {LBRACK expression RBRACK} {LBRACK RBRACK}

literal ::= INT_LITERAL | DOUBLE_LITERAL | CHAR_LITERAL | STRING_LITERAL
          | TRUE        | FALSE          | NULL
